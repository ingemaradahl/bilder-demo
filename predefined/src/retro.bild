/**
 * Retro (instagrammy) effect with border.
 *
 * Inputs:
 *  img: Image on which effect is applied
 *  border: Border image with red channel for opacity. Try using
 *  'images/border.png'
 */

Color vignette(Image img, Float x, Float y) {
	Color c = img(x,y);
	Float d = distance(Point(x,y), Point(0.5, 0.5));

	c.rgb = c.rgb * smoothstep(0.95, 0.45, d);
	return c;
}

Color applyMask(Image img, Image mask, Float x, Float y) {
	Color c = img(x,y);
	c.rgb = c.rgb - RGB(mask(x,y).r);

	return c;
}

Color retro(Image img, Float x, Float y) {
	const RGB fuzz = RGB(0.98, 0.8, 0.44);
	const RGB offset = RGB(0.0, 0.2, 0.4);

	Color matten(Float x, Float y) {
		Point p = Point(x,y) * 1.04 - 0.02;
		RGB c = img(p).rgb;

		c.r = img(p+0.001).r;
		c = min(c*fuzz*1.3, fuzz);
		c += offset * (1.0-c);

		return Color(c, 1.0);
	};

	return vignette(matten, x, y);
}

Color main(Float x, Float y) {
	external Image img;
	external Image border;

	return applyMask(retro(img), border, x, y);
}

